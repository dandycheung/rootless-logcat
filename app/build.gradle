plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 36
    defaultConfig {
        applicationId "com.tananaev.logcat"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 16
        versionName "2.2"
        multiDexEnabled true
    }
    namespace "com.tananaev.logcat"

    buildFeatures {
        flavorDimensions = ['default']
    }
    productFlavors {
        regular {
            isDefault = true
            ext.enableCrashlytics = false
        }
        google
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'com.tananaev:adblib:1.3'
    googleImplementation platform('com.google.firebase:firebase-bom:33.16.0')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-ads:24.4.0'
    googleImplementation 'com.google.android.play:review-ktx:2.0.2'
}

tasks.register('copyFirebaseConfig', Copy) {
    from '../../environment/firebase'
    into '.'
    include 'rootless-logcat.json'
    rename('rootless-logcat.json', 'google-services.json')
}
afterEvaluate {
    tasks.matching { it.name.contains('Google') }.configureEach { task ->
        if (task.name.contains('Regular')) {
            task.enabled false
        } else {
            task.dependsOn copyFirebaseConfig
        }
    }
}
